allprojects {
    //borrowed from Jake Wharton's gist
    //https://gist.github.com/JakeWharton/2066f5e4f08fbaaa68fd
    // ensures we do not ever use wildcards
    // if lib author has used + wildcard than its
    // resolutionStrategy.force "group:name:exact_version"
    afterEvaluate { project ->
        //noinspection GroovyAssignabilityCheck
        project.configurations.all {

            resolutionStrategy.eachDependency { DependencyResolveDetails details ->
                //noinspection GroovyAssignabilityCheck
                def requested = details.target
                if (requested.version.contains('+')) {
                    throw new GradleException("Wildcard dependency forbidden: ${requested.group}:${requested.name}:${requested.version}")
                }
            }

            //resolution strategy for support annotations being out of synch
            resolutionStrategy.force 'com.android.support:support-annotations:23.1.0'
            //resolution strategy for support libs, ie when we use rxpresso for example

        }
    }
    repositories {
        jcenter()
        //for getting libs deployed to jitpack
        maven { url "https://jitpack.io" }
    }
    tasks.withType(JavaForkOptions) {
        // Forked processes like GradleWorkerMain for tests won't steal focus!
        jvmArgs '-Djava.awt.headless=true'
    }
    // for jdk8 to avoid the R.java errors
    if (JavaVersion.current().isJava8Compatible()){
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

}

ext {
    //android
    androidBuildToolsVersion = "23.0.2"
    androidMinSdkVersion = 16
    androidTargetSdkVersion = 23
    androidCompileSdkVersion = 23

}